/*
This program (The Chameleon Mini Live Debugger) is free software written by
Maxie Dion Schmidt: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

The complete license provided with source distributions of this library is
available at the following link:
https://github.com/maxieds/ChameleonMiniLiveDebugger
*/

apply plugin: 'com.android.application'

import java.text.DateFormat
import java.text.SimpleDateFormat

def getDateTimestamp() {
    DateFormat df = new SimpleDateFormat("YYYY-MM-dd-HHmmss");
    return new String(df.format(new Date()).toString());
}

def getShellCommandOutput(String shellCmd) {
    def cmdResult = ['bash', '-c', shellCmd].execute();
    cmdResult.waitFor();
    return cmdResult.text[0..-2];
}

def getGitCommitHash() {
    return getShellCommandOutput("git show | head -n 1 | sed -e \"s/commit //\" | cut -c-12");
}

def getGitCommitDate() {
    return getShellCommandOutput("git show | grep Date: | head -n 1 | sed -e \"s/Date:[ ]*//\"");
}

android {

    def configBuildTimeStamp = getDateTimestamp()
    defaultConfig {

        applicationId "com.maxieds.chameleonminilivedebugger"
        minSdkVersion 29
        targetSdkVersion 29
        compileSdkVersion 29
        versionCode 93 + 8080
        versionName "1.3.1"
        multiDexEnabled true

        buildConfigField "String", "GIT_COMMIT_HASH", "\"" + getGitCommitHash() + "\""
        buildConfigField "String", "GIT_COMMIT_DATE", "\"" + getGitCommitDate() + "\""

        setProperty("archivesBaseName", "ChameleonMiniLiveDebugger-v$versionName-$configBuildTimeStamp")

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            crunchPngs false
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            crunchPngs false
        }
    }
    buildToolsVersion '30.0.1'

    flavorDimensions "mode"
    productFlavors {
        free {
            versionNameSuffix "-free"
            dimension "mode"
            buildConfigField "boolean",            "PAID_APP_VERSION", "false"
            buildConfigField "boolean",            "REPORT_CRASHES", "false"
            buildConfigField "String",             "BUILD_TIMESTAMP", "\"" + configBuildTimeStamp + "\""
            buildConfigField "java.util.Locale",   "DEFAULT_LOCALE",  "java.util.Locale.getDefault()"
            resConfigs       "en", "en_US", "en_UK", "de", "fr", "ru"
            manifestPlaceholders = [
                    appLabelDesc       : "@string/app_display_short_name",
                    appIcon            : "@mipmap/chameleon_app_icon_round",
                    appRoundIcon       : "@mipmap/chameleon_app_icon_round",
                    appTheme           : "@style/AppThemeGreen",
                    appDebug           : "false",
                    installLocation    : "preferExternal" // "internalOnly"
            ]
        }
        paid {
            applicationIdSuffix ".paid"
            versionNameSuffix "-paid"
            buildConfigField "boolean",            "PAID_APP_VERSION", "true"
            buildConfigField "boolean",            "REPORT_CRASHES", "false"
            buildConfigField "String",             "BUILD_TIMESTAMP", "\"" + configBuildTimeStamp + "\""
            buildConfigField "java.util.Locale",   "DEFAULT_LOCALE",  "Locale.default"
            resConfigs       "en", "en_US", "en_UK", "de", "fr", "ru"
            dimension "mode"
            manifestPlaceholders = [
                    appLabelDesc       : "@string/app_display_short_name_paid",
                    appIcon            : "@mipmap/chameleon_app_icon_round_paid",
                    appRoundIcon       : "@mipmap/chameleon_app_icon_round_paid",
                    appTheme           : "@style/AppThemeGreenPaid",
                    appDebug           : "false",
                    installLocation    : "auto"
            ]
        }

        packagingOptions {
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/license.txt'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/NOTICE.txt'
            exclude 'META-INF/notice.txt'
            exclude 'META-INF/ASL2.0'
            exclude 'META-INF/androidx*'
            exclude 'META-INF/android*'
            exclude 'META-INF/com.google.android.*'
        }

    }

    dependencies {
        testImplementation 'junit:junit:4.12'
        implementation fileTree(include: ['*.jar'], dir: 'libs')
        implementation 'com.android.support.constraint:constraint-layout:2.0.2'
        implementation 'androidx.legacy:legacy-support-v4:1.0.0'
        implementation 'androidx.appcompat:appcompat:1.2.0'
        implementation 'com.google.android.material:material:1.3.0-alpha03'
        implementation 'org.apache.commons:commons-text:1.7'
        implementation 'org.antlr:antlr4-runtime:4.7'
        implementation 'com.github.felHR85:UsbSerial:4.5.2'
        implementation 'com.shawnlin:number-picker:2.4.9'
        implementation 'com.github.maxieds:AndroidFilePickerLight:release-v8'
    }

    repositories {
        jcenter()
        google()
        mavenCentral()
        maven {
            url "https://maven.google.com"
            name 'Google'
        }
        maven { url "https://jitpack.io" }
    }

}